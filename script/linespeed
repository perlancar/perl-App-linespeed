#!perl

# DATE
# VERSION

use strict;
use warnings;

use Getopt::Long::Complete;
use Time::HiRes qw(time);

my %Opts = (
);

sub _with_unit {
    my ($n2, $n1, $t2, $t1) = @_;
    if ($t2 == $t1) {
        return (0, "");
    } else {
        my $s = ($n2-$n1) / ($t2-$t1);
        if ($s > 1.5e9) {
            return ($s/1e9, "Gi/s");
        } elsif ($s > 1.5e6) {
            return ($s/1e6, "Mi/s");
        } elsif ($s > 1.5e3) {
            return ($s/1e3, "Ki/s");
        } else {
            return ($s, "/s");
        }
    }
}

sub parse_cmdline {
    my $res = GetOptions(
        'version|v'     => sub {
            no warnings;
            print "linespeed version $main::VERSION ($main::DATE)\n";
            exit 0;
        },
        'help|h'        => sub {
            print <<USAGE;
Usage:
  linespeed [OPTIONS]... < INPUT
  linespeed --version, -v
  linespeed --help, -h
Options:
For more details, see the manpage/documentation.
USAGE
            exit 0;
        },
    );
    exit 99 if !$res;
}

sub run {
    $|++;
    my $num_lines = 0;
    my $prev_time = time();
    my $prev_num_lines;
    my ($first_num_lines, $first_time);
    my $msg = "";
    while (<>) {
        $num_lines++;

        my $time = time();

	unless (defined $prev_num_lines) {
            $prev_num_lines = $num_lines;
            $prev_time = time();
            $first_num_lines = $num_lines;
            $first_time = $prev_time;
            next;
        }

        # don't report speed too often
        next unless $time - $prev_time >= 0.5;

        my $new_msg = sprintf(
            "Cur speed: %8.3f%-4s  Avg: %8.3f%-4s",
            _with_unit($num_lines, $prev_num_lines , $time, $prev_time ),
            _with_unit($num_lines, $first_num_lines, $time, $first_time),
        );

        print "\b" x length $msg;
        print $new_msg, "\e[K"; # clear to EOL

        $msg = $new_msg;
        $prev_num_lines = $num_lines;
        $prev_time = $time;
    }
}

# MAIN

parse_cmdline();
run();

1;
# ABSTRACT: Calculate how fast input lines are coming in
# PODNAME:

=head1 SYNOPSIS

 % tail -f some-log | linespeed
 Cur speed: 33.3/s  Avg: 31.6/s_


=head1 DESCRIPTION

C<linespeed> receives lines from standard input (or files) and calculates how
fast the lines are coming in. It can be used to measure, e.g. the current HTTP
requests/sec a server is getting from the webserver access log.


=head1 OPTIONS


=head1 TODO

Add option: --si to use power of 1000 not 1024 (but 1000 is the default).

Show speed in the last minute, 5 mins, 15 mins (or customizable).


=head1 HISTORY

First written in 2000-04-14.


=head1 SEE ALSO

speedometer, L<https://excess.org/speedometer/>

=cut
